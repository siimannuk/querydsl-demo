sourceSets {
  main {
    java {
      srcDirs = ['src/main/java']
    }
    resources {
      srcDirs = ['src/main/resources']
    }
  }
  generated {
    java {
      srcDirs = ['src/generated/java']
    }
  }
  test {
    resources {
      srcDirs = ['src/test/resources', 'db']
    }
  }
}

idea {
  module {
    sourceDirs += file(sourceSets.generated.java.srcDirs[0])
  }
}

dependencies {
  implementation (
    libraries.spring.boot_starter_jpa,
    libraries.spring.boot_starter_json,
    libraries.hibernate,
    libraries.postgresql,
    libraries.querydsl.jpa, // adds Querydsl support
    libraries.commons.io
  )

  annotationProcessor (
    "${libraries.querydsl.apt}:jpa", // generates Querydsl classes
    libraries.hibernate
  )

  testImplementation (
    libraries.spring.boot_test,
    libraries.p6spy
  )
}

compileJava {
  options.annotationProcessorGeneratedSourcesDirectory = sourceSets.generated.java.srcDirs[0]
}

compileGeneratedJava {
  classpath += sourceSets.main.runtimeClasspath
}

clean {
  delete sourceSets.generated.java.srcDirs[0]
}
